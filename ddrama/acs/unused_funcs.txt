// -------------------------->>>>
// COOKBOOK INFO:
// Some of the above funcs are
// modified from existing ZDoom
// ACS functions, with some cleanup.
//
// Any original or heavily modified
// functions will have the func_*
// prefix. The exception is the
// GetAspectRatio function, because
// I wanted to make it distinguishable
// from the abs function
// -------------------------->>>>

// Get Aspect Ratio Information
// TODO: Find some use for it
function int func_GetAspectRatio(void)
{
	int width = GetCVar("vid_defwidth");
	int height = GetCVar("vid_defheight");
	int nowidescreen = GetCVar("vid_nowidescreen");
	int tft = GetCVar("vid_tft");
	int aspect = GetCVar("vid_aspect");

	switch (aspect)
	{
		case 1:
			return ASPECT_16_9;
		case 2:
			return ASPECT_16_10;
		case 3:
			return ASPECT_4_3;
		case 4:
			return ASPECT_5_4;
	}
	
	if (nowidescreen)
	{
		if(!tft) return ASPECT_4_3;
		
		if(FixedMul(height<<16, FixedDiv(5.0, 4.0)) == width<<16) return ASPECT_5_4;
		
		return ASPECT_4_3;
	}
	
	if (abs((abs(FixedMul(height<<16, FixedDiv(16.0, 9.0)))>>16) - width) < 10) return ASPECT_16_9;
	
	if (abs((abs(FixedMul(height<<16, FixedDiv(16.0, 10.0)))>>16) - width) < 60)
	{
		if((width == 320 && height == 200) || (width == 640 && height == 400)) return ASPECT_4_3;
		
		return ASPECT_16_10;
	}
	
	if (FixedMul(height<<16, FixedDiv(5.0, 4.0))>>16 == width && tft) return ASPECT_5_4;
	
	return ASPECT_4_3;
}

function void func_CompassDirGet(void)
{
	int _ang, _dirstr;

	_ang = (GetActorAngle(0) >> 8);

	if ((_ang >= 0 && _ang < 12) || (_ang >= 240 && _ang <= 255))
	{
		_dirstr = StrParam(s:"E");
	}
	else if (_ang >= 12 && _ang < 48)
	{
		_dirstr = StrParam(s:"NE");
	}
	else if (_ang >=48 && _ang < 72)
	{
		_dirstr = StrParam(s:"N");
	}
	else if (_ang >= 72 && _ang < 112)
	{
		_dirstr = StrParam(s:"NW");
	}
	else if (_ang >= 112 && _ang < 144)
	{
		_dirstr = StrParam(s:"W");
	}
	else if (_ang >= 144 && _ang < 176)
	{
		_dirstr = StrParam(s:"SW");
	}
	else if (_ang >= 176 && _ang < 208)
	{
		_dirstr = StrParam(s:"S");
	}
	else if (_ang >= 208 && _ang < 240)
	{
		_dirstr = StrParam(s:"SE");
	}

	_playerDir[PlayerNumber()] = _dirstr;
}


// The Kumasister Enemy can steal your gun.
// It needs to know what gun you have to decide
// wether to take it or not. It's not interested
// in shotguns, pistols, wacom pens.
script "DDLib_CheckPlayerGun" (void)
{
	int rv = 0;

	if (SetActivatorToTarget(0))
	{
		if(CheckWeapon("DD_RocketLauncher") && CheckInventory("DD_RocketLauncher"))
		{
			TakeInventory("DD_RocketLauncher", 1);
			rv = 1;
		}
		else if(CheckWeapon("DD_Minigun") && CheckInventory("DD_Minigun"))
		{
			TakeInventory("DD_Minigun", 1);
			rv = 2;
		}
		else
			rv = 0;
	}
	else
		rv = -1;

	SetResultValue(rv);
}

