/* *******************************************************************
 *		marketspeaker.zsc
 *
 *		Supermarket Speaker
 *
 *		As long as you attribute it to me in some
 *		way I don't mind people using this. Considering how much
 *		some people have caused an uproar over the lack of proper
 *		examples and the like.
 *
 *		Copyright Sanya K. C. Waffles 2013-2017
 *
 * ****************************************************************** */

Class DD_SupermarketSpeaker : DD_Decoration
{
	Default
	{
		//$Sprite SPKRA0
		//$Title "Supermarket Speaker"
		//$Category "Decoration"
		//$NotAngled
		//$Color 4
		
		Radius 32;
		Height 1;
		VSpeed 0;
		Health 2;
		Scale 0.25;
		
		-Solid
		+SpawnCeiling
		+NoGravity
		+Shootable
		+NoBlood
		+DontGib
		+DontThrust
		+NoTeleport
		+DontFall
		+FlatSprite
	}

	States
	{
		Spawn:
			SPKR A 105 NoDelay
			{
				A_LookEx((LOF_NOSOUNDCHECK | LOF_NOSEESOUND), 0, 0, 0, 360, "See");
				A_Stop();
			}
			Loop;
		
		See:
			SPKR A 350
			{
				A_Stop();
				A_PlaySound("musak/sight", CHAN_VOICE);
			}
			SPKR A 12
			{
				A_StopSound(CHAN_VOICE);
				A_Stop();
			}
			Goto Spawn;
		
		Death:
			SPKR B 105
			{
				A_PlaySound("misc/tcanbreak", CHAN_BODY);
				A_PlaySound("musak/death", CHAN_VOICE);
				
				Actor mo = null;
				int i;
				for(i = (random[TVDebris]()&3)+3; i; i--)
				{
					mo = Spawn("DD_MetalJunk", Pos, ALLOW_REPLACE);
					if (mo)
					{
						mo.Vel.X = random2[TVDebris]() / 64.;
						mo.Vel.Y = random2[TVDebris]() / 64.;
						mo.Vel.Z = ((random[TVDebris]() & 7) + 5) * 0.75;
					}
				}
			}
			SPKR B -1;
			Stop;
	}
}