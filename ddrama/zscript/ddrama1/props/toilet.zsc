/* *******************************************************************
 *		toilet.zsc
 *
 *		Toilet
 *
 *		As long as you attribute it to me in some
 *		way I don't mind people using this. Considering how much
 *		some people have caused an uproar over the lack of proper
 *		examples and the like.
 *
 *		Copyright Sanya K. C. Waffles 2013-2017
 *
 * ****************************************************************** */

Class DD_Toilet : DD_SwitchableDecoration
{
	Default
	{
		//$NotAngled
		//$Title "Toilet"
		//$Sprite JOHNA0
		//$Category "Obstacles"
		
		Height 40;
		Radius 12;
		Health 5;
		Mass 1000;
		Activation THINGSPEC_Default | THINGSPEC_Switch | THINGSPEC_NoDeathSpecial;
		
		+Solid
		+UseSpecial
		+Shootable
		+NoBlood
		+DontGib
		+DontFall
	}
	
	void A_ToggleToiletStart()
	{
		A_DisableSpecial();
		A_PlaySound("misc/toiletflush", CHAN_BODY);
	}
	
	void A_ToiletXPlode()
	{
		A_PlaySound("misc/tcanbreak", CHAN_BODY);
		A_DisableSpecial();
		A_NoBlocking();
		
		Actor mo = null;
		int i;
		for(i = (random[Toilet]() & 3) + 3; i; i--)
		{
			mo = Spawn("DD_MetalJunk", pos, ALLOW_REPLACE);
			if (mo != null)
			{
				mo.Vel.X = random2[Toilet]() / 64.;
				mo.Vel.Y = random2[Toilet]() / 64.;
				mo.Vel.Z = ((random[Toilet]() & 3) + 5) * 0.75;
			}
		}
		A_SpawnItemEx("DD_WaterGush");
	}
	
	States
	{
		Active:
		Inactive:
			JOHN A 1 A_ToggleToiletStart;
			JOHN A 38;
			JOHN A 1 A_EnableSpecial;
			Goto Spawn;
		
		Spawn:
			JOHN A 1;
			Loop;
		
		Death:
			JOHN B 1 A_ToiletXPlode;
			JOHN B -1;
			Stop;
	}
}

