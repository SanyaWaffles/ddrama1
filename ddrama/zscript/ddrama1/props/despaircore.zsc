/* *******************************************************************
 *		despaircore.zsc
 *
 *		Despairium Core
 *
 *		As long as you attribute it to me in some
 *		way I don't mind people using this. Considering how much
 *		some people have caused an uproar over the lack of proper
 *		examples and the like.
 *
 *		Copyright Sanya K. C. Waffles 2013-2017
 *
 * ****************************************************************** */

Class DD_DespairiumCore : DD_Prop
{
	Default
	{
		//$NotAngled
		//$Title "Despairium Core"
		//$Sprite CORUA0
		//$Category "Obstacles"
		
		Health 1000;
		Radius 24;
		Height 96;
		Mass 9000;
		Speed 0;
		Gravity 10.0;
		DeathSound "world/barrelx";
		DamageType "Despair";
		Obituary "$OB_DESPAIRCORE";

		+Shootable
		+NoBlood
		+DontGib
		+DontThrust
	}

	const DESPAIRIUM_CORE_TIMER = 50;
	
	int coretimer;
	
	override void BeginPlay()
	{
		// init these variables
		coretimer = DESPAIRIUM_CORE_TIMER;
	}
	
	state A_DespairiumExplosionLoop()
	{
		A_StopSound(CHAN_BODY);
		
		if (coretimer == DESPAIRIUM_CORE_TIMER)
		{
			A_Scream();
			A_Explode();
		}
		
		A_SpawnItemEx("DD_DespairiumExplosion", 0,   0, ((height/2) + random[CoreBoom](12, 24)), flags:SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DD_DespairiumExplosion", 0, -16, ((height/2) + random[CoreBoom](12, 24)), flags:SXF_NOCHECKPOSITION);
		A_SpawnItemEx("DD_DespairiumExplosion", 0,  16, ((height/2) + random[CoreBoom](12, 24)), flags:SXF_NOCHECKPOSITION);
		
		coretimer -= random[CoreTimer](3, 5);
		
		return (coretimer <= 0) ? ResolveState("DeathFinal") : ResolveState(null);
	}

	States
	{
		Spawn:
			CORU A 0 NoDelay A_PlaySound("despaircore/hum", CHAN_BODY, 1.5, true);
		SpawnLoop:
			CORU ABCDCB 4 Bright;
			Loop;
		
		Death:
			CORU E 5 Bright A_DespairiumExplosionLoop;
			Loop;
		
		DeathFinal:
			CORU E 105 A_SetRenderstyle(0.4, STYLE_Translucent);
			CORU E -1;
			Stop;
	}
}

Class DD_DespairiumExplosion : DD_Decoration
{
	Default
	{
		VSpeed 4;
		Speed 0;
		Radius 16;
		Height 16;
		Gravity 0.0;
		Scale 0.1;
		Renderstyle "Translucent";
		Alpha 0.7;

		+NOGRAVITY
	}

	States
	{
		Spawn:
			CORX A 3 Bright;
			CORX B 3 Bright A_Quake(7, 20, 0, 5120);
			CORX C 2 A_FadeOut(0.2, true);
			Wait;
	}
}