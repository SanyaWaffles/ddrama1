/* *******************************************************************
 *		base_player.zsc
 *
 *		Base player pawn Daytime Drama Class
 *
 *		As long as you attribute it to me in some
 *		way I don't mind people using this. Considering how much
 *		some people have caused an uproar over the lack of proper
 *		examples and the like.
 *
 *		Copyright Sanya K. C. Waffles 2013-2017
 *
 * ****************************************************************** */

enum EVoiceOverTypes
{
	VOX_None = -1,
	VOX_Normal = 0,
	VOX_Boss1,
	VOX_Boss2,
	VOX_Boss3,
	VOX_Ending,
};

Class DD_PlayerPawn : PlayerPawn
{
	Default
	{
		TeleFogSourceType "DD_TeleportFog";
		TeleFogDestType "DD_TeleportFog";
		
		Scale 0.12;
	}
	
	// is this even a thing we need?
	enum ETrollBloodColors
	{
		TROLL_BLOOD_OLIVE,
		TROLL_BLOOD_BRONZE,
		TROLL_BLOOD_COBALT,
		TROLL_BLOOD_MAX,
	};
	
	bool startepisode;		// starting episode?
	bool heatseektoggle;	// Rocket Launcher Heatseek Toggle
	int trollbloodstage;	// Trollblood Device Stage
	int shoucounter;		// current genicdershou counter (general use)
	int cscenehold;			// Hmmm...
	int expandloop;			// Expand Count
	int onelinercount;		// One Liner Counter
	bool onelinerhit;		// One Liner Flag
	int onelinertype;		// One Liner Type
	int onelinercooldown;	// One Liner cooldown;
	int typeofrip;			// Type of high quality RIP
	int seconds;			// Seconds elapsed
	
	// Make a rubbery expanding noise
	void A_DDExpandDongStart()
	{
		A_PlaySound("generic/expanding", CHAN_BODY);
	}
	
	// Loop until you go big
	state A_DDExpandDongLoop()
	{
		expandloop++;
		
		return (expandloop >= DD_ENEMY_SCALECOUNT) ? ResolveState("ExpandFinish") : ResolveState(null);
	}
	
	// When you do go big, explode like a motherfuckin' mushroom cloud
	void A_DDExpandDongFin()
	{
		A_Explode(128, 144);
		A_DDSpawnLudicrousGibs();
	}
	
	// Throw actual giblets around
	void A_DDSpawnLudicrousGibs()
	{
		A_SpawnItemEx("DD_LudicrousGibsFx", flags:DDFL_AMMOFLAGSFX);
	}
	
	void DDFunc_PlayerDies()
	{
		A_NoBlocking();
		A_PlayerScream();
		A_SetRoll((roll + (RandomPick[PlayerRIP](-22, -11, 11, 22))), SPF_INTERPOLATE);
		A_SetPitch((pitch + (Random[PlayerRIP](1,5))), SPF_INTERPOLATE);
	}
	
	void DDFunc_PlayerDiesXTreem()
	{
		A_NoBlocking();
		A_XScream();
		A_SetRoll((roll + (randompick[DDPLAYER_ROLL](-22, -11, 11, 22))), SPF_INTERPOLATE);
		A_SetPitch((pitch + (random[DDPLAYER_PITCH](1,3))), SPF_INTERPOLATE);
		A_DDSpawnLudicrousGibs();
	}
	
	void A_DDPlayerDiesGoesBig()
	{
		DDFunc_PlayerDiesXTreem();
		A_PlaySound("world/barrelx", CHAN_BODY);
		A_Explode(128, 144);
	}
	
	void A_NewPlayerPain()
	{
		A_PlaySound("sanya/sarahpain", CHAN_6);
	}
	
	void A_OneLinerInit(int olt)
	{
		onelinercount = 150;
		if (olt > VOX_None && olt <= VOX_Ending)
			onelinertype = olt;
	}
	
	// Perform Voice One Liner Actions
	void A_OneLinerProcess()
	{
		if (onelinercount >= 150 && onelinercooldown <= 0 && health > 0)
		{
			onelinerhit = true;
			
			if (onelinertype == VOX_Normal)
			{				
				A_PlaySound("sanya/sarahvoiceover", (DDPLAYER_VOICECHAN), 4.0, false, ATTN_NONE);
				onelinercooldown = 150;
			}
		}
		
		if (onelinerhit)
		{
		
			if (onelinercount > 0)
				onelinercount--;
			
			if (onelinercount <= 0)
			{
				onelinercount = 0;
				onelinertype = VOX_None;
				onelinerhit = false;
			}
		}
	}
	
	void PerSecondFunctions()
	{;
		A_OneLinerProcess();
	}
	
	override bool CanCrouch() const
	{
		return player.morphTics <= 1 || bCrouchableMorph;
	}
	
	// This is for stomping action... seems a bit goofy at the moment
	override bool CanCollideWith(Actor other, bool passive)
	{
		if (other && other.bIsMonster && other.pos.z < pos.z && !other.bBoss)
		{
			A_Explode(6, 6, XF_NOTMISSILE);
		}
		
		return Super.CanCollideWith(other, passive);
	}
	
	override void BeginPlay()
	{
		if (!startepisode)
		{
			startepisode = true;
			heatseektoggle = false;
			trollbloodstage = 0;
		}
		expandloop = 0;
		
		Super.BeginPlay();
	}
	
	override void Tick()
	{
		// to avoid something going on with "voodoo dolls" not performing right
		// or the like. Since Daytime Drama doesn't support multiplayer, this
		// is more for the sake of completion, or if someone wants to
		// expand on it.
		if (!player || !player.mo || player.mo != self)
		{
			// Only perform the PlayerPawn functionality, nothing more...
			Super.Tick();
			return;	// bailing out...
		}
		
		// This is mainly here to do things on every ~second or so.
		// We use modulo to check if current tics can be divided by 35
		// If it can, execute PerSecond Functions
		// ----
		if (level.time % 35 == 0)
		{
			// any per-second functions go here
			PerSecondFunctions();
		}
		
		if (onelinercooldown > 0)
			onelinercooldown--;
			
		if (!CountInv("DD_SyoPepperPowerStrength"))
			bDontMorph = false;
			
		Super.Tick();	// call any PlayerPawn Tick functionality
	}
}

