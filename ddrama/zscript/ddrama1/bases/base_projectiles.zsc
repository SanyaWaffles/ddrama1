/* *******************************************************************
 *		base_projectiles.zsc
 *
 *		Base projectile/missile Daytime Drama Classes
 *
 *		As long as you attribute it to me in some
 *		way I don't mind people using this. Considering how much
 *		some people have caused an uproar over the lack of proper
 *		examples and the like.
 *
 *		Copyright Sanya K. C. Waffles 2013-2017
 *
 * ****************************************************************** */

Class DD_ProjectileActor : DD_Actor
{
	Default
	{
		Mass 0;
		Speed 8;
		RenderStyle "Normal";
		Alpha 1;
		
		Projectile;
		
		+ActivateImpact
		+ActivatePCross
	}
}

// Morph projectiles have a few
// properties and their own base
// class, so we need this for scales
// and flags
Class DD_MorphProjectile : MorphProjectile
{
	Default
	{
		Speed 18;
		Height 12;
		Radius 12;
		Scale 0.12;

		-ActivatePCross
		-ActivateImpact
	}
	
		
	// Thank FishyClockwork and Marisa Kirisame for this
	override int DoSpecialDamage(Actor target, int damage, Name damagetype)
	{
		let armor = BasicArmor(target.FindInventory("BasicArmor"));
		if ( !armor )
			return Super.DoSpecialDamage(target, damage, damagetype);
		
		// Save armor info
		let savepercent = armor.SavePercent;
		let amount = armor.Amount;
		let maxamount = armor.MaxAmount;
		let icon = armor.Icon;
		let maxabsorb = armor.MaxAbsorb;
		let maxfullabsorb = armor.MaxFullAbsorb;
		let armortype = armor.ArmorType;
		let actualsaveamount = armor.ActualSaveAmount;
		
		if (target.player)
		{
			target.player.mo.morphplayer(NULL, PlayerClass, Duration, MorphStyle, MorphFlash, UnMorphFlash);
		}
		else
		{
			target.morphmonster(MonsterClass, Duration, MorphStyle, MorphFlash, UnMorphFlash);
		}
		
		//Restore information
		armor.SavePercent = savepercent;
		armor.Amount = amount;
		armor.MaxAmount = maxamount;
		armor.Icon = icon;
		armor.MaxAbsorb = maxabsorb;
		armor.MaxFullAbsorb = maxfullabsorb;
		armor.ArmorType = armortype;
		armor.ActualSaveAmount = actualsaveamount;
		
		return -1;
	}
}