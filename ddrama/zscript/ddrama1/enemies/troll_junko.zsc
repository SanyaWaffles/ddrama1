/* *******************************************************************
 *		troll_junko.zsc
 *
 *		Junko Alternian Troll
 *
 *		As long as you attribute it to me in some
 *		way I don't mind people using this. Considering how much
 *		some people have caused an uproar over the lack of proper
 *		examples and the like.
 *
 *		Copyright Sanya K. C. Waffles 2013-2017
 *
 * ****************************************************************** */

Class DD_JunkoMorphShot : DD_ShrinkShot
{
	Default
	{
		MorphProjectile.Duration 175;
	}
}

Class DD_TrollJunko : DD_Enemy
{
	Default
	{
		//$Sprite JTRLA0
		//$Title "wardrobeMelancholia"
		
		Health 420;
		Radius 16;
		Height 54;
		Mass 150;
		Speed 12;
		DamageFactor "Normal", 0.8;
		DamageFactor "Rocket", 0.8;
		DamageFactor "Despair", 0.4;
		PainChance 128;
		PainChance "Despair", 0;
		Species "Troll";
		BloodColor "00 cc 00";
		SeeSound "troll/sight";
		PainSound "troll/pain";
		DeathSound "troll/death";
		ActiveSound "troll/active";
		Obituary "$DD_OB_TROLLJUNKO";

		+DontMorph
	}
	
	void A_DDTrollJunkoDeath()
	{
		A_ScreamAndUnblock();
		A_DDGivePlayerOneLiner();
	}

	States
	{
		Spawn:
			JTRL A 8 A_DDramaLook;
			Loop;
		
		See:
			JTRL BCDEBCDE 3 A_DDramaChase;
			Loop;
		
		Melee:
			JTRL I 1 A_FaceTarget;
			JTRL J 2 A_CustomMeleeAttack(8, "troll/melee", "troll/miss");
			JTRL K 1;
			JTRL K 0 A_JumpIfTargetInsideMeleeRange("Melee");
			Goto See;
		
		Missile:
			JTRL F 0 A_PlaySound("troll/hiss", CHAN_VOICE);
			JTRL F 14 A_FaceTarget;
			JTRL F 0 A_JumpIfTargetInLOS("MissileActual");
			Goto See;
		
		MissileActual:
			JTRL F 0 A_JumpIfCloser(196, "Lunge");
			JTRL FG 4 A_FaceTarget;
			JTRL H 1 Bright A_SpawnProjectile("DD_JunkoMorphShot", 48, 0);
			JTRL H 3 Bright;
			Goto See;
			
		Lunge:
			JTRL B 0 A_FaceTarget;
			JTRL B 2 ThrustThing(angle*256/360, 16, 0, 0);
			JTRL B 0
			{
				ThrustThingZ(0, 16, 1, 1);
				return A_JumpIfTargetInsideMeleeRange("Melee");
			}
			Goto Lunge;
		
		Pain:
			JTRL L 2;
			JTRL L 2 A_Pain;
			Goto See;
		
		Death:
		Death.Rocket:
		Death.Webbing:
		Death.Shou:
		Death.Extreme:
			JTRL M 5 A_DDTrollJunkoDeath;
			JTRL N 5;
			JTRL O 5;
			JTRL P -1 A_DDGivePlayerOneLiner(2);
			Stop;
		
		Death.Expand:
			JTRL L 1;
			Goto Super::Death.Expand;
			
		Crush:
			GIBY A 0 A_PlaySound("misc/crushed1", CHAN_BODY);
			GIBY A -1;
			Stop;
		
		Raise:
			Stop;
	}
}