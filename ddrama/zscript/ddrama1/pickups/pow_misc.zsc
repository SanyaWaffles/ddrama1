/* *******************************************************************
 *		pow_misc.zsc
 *
 *		Misc. Powerups
 *
 *		As long as you attribute these functions to me in some
 *		way I don't mind people using these. Considering how much
 *		some people have caused an uproar over the lack of proper
 *		examples and the like.
 *
 *		Copyright Sanya K. C. Waffles 2013-2017
 *
 * ****************************************************************** */

Class DD_Handbag : BackpackItem
{
	Default
	{
		//$NotAngled
		//$Category "Ammunition"
		//$Color 6
		//$Sprite HBAGA0
		//$Title "Ammo satchel"
		
		Scale 0.12;
		Radius 24;
		Height 36;

		Inventory.PickupMessage "$DD_GOTHBAG";
		Inventory.PickupSound "pickups/generic";
	}

	States
	{
		Spawn:
			HBAG A -1;
			Stop;
	}
}

// Christmas Cracker
// Like ill-conceived love:: one loud bang, 
// a dissapointing trinket, and ultimately a joke
Class DD_Cracker : CustomInventory
{
	Default
	{
		//$Sprite CRKRA0
		//$Title "Cracker"
		//$Category "Powerups"
		
		Scale 0.12;
		Radius 12;
		Height 24;
		Inventory.PickupMessage "$DD_GOTCRACKER";
		Inventory.PickupSound "pickups/cracker";
		Inventory.Amount 1;

		-Inventory.InvBar;
	}
	
	States
	{
		Spawn:
			CRKR A -1;
			Stop;
			
		Pickup:
			CRKR A 0
			{
				static const Class<Actor> trinkets[] =
				{
					"DD_DroppedPistolBullets",
					"DD_DroppedShells",
					"DD_RocketAmmo",
					"DD_OliveBloodAmmo",
					"DD_ChocolateBar",
					"DD_SyoPepper",
					"DD_AmmoBelt"
				};
				
				String sr;
				
				int joke = Random[CrackerJoke](1, 16);
				
				sr.AppendFormat("$%s%02d", "DD_CRACKERJOKE", joke);
				
				A_Print(StringTable.Localize(sr));
				
				A_SpawnItemEx(trinkets[Random[CrackerJoke](0, 6)], flags: SXF_NOCHECKPOSITION);
				
				return true;
			}
			Stop;
	}
}