/* *******************************************************************
 *		tv.zsc
 *
 *		Dudebro Television Breaker for Daytime Drama
 *
 *		As long as you attribute it to me in some
 *		way I don't mind people using this. Considering how much
 *		some people have caused an uproar over the lack of proper
 *		examples and the like.
 *
 *		Copyright Sanya K. C. Waffles 2013-2017
 *
 * ****************************************************************** */

/* Dudebro Television Breaker calls ACS cookbook library script on death
 *
 *					DDLib_BreakTVReg
 *
 * It causes the line ID (arg0) to "break"
 *
 * arg1 is the portion to change (facing front).
 *
 * arg2 is optional and deletes the object.
 *
 */
Class DD_RegularTVController : DD_ShootableController
{
	Default
	{
		//$Sprite internal:TV_Ctrl
		//$Title "Regular TV"
		//$Category "Controllers"
		//$NotAngled
		//$Arg0 "Line To Break"
		//$Arg0Default 0
		//$Arg0Tooltip "Changes Front Texture of Given Line ID"
		//$Arg0Type 15
		//$Arg1 "Sidedef Portion"
		//$Arg1Default 0
		//$Arg1Tooltip "Portion of side to change"
		//$Arg1Type 11
		//$Arg1Enum { 0 = "Top"; 1 = "Middle"; 2 = "Lower"; }
		//$Arg2 "MapSpot Tag (Sound)"
		//$Arg2Tooltip "If you use a mapspot to\nemit noise from a TV, use this\nto remove it."
		//$Arg2Type 0
		
		Radius 20;
		Height 40;
	}
	
	
	void A_ChangeTVTextureBroken(int lineid, int sid, int sidepos)
	{
		if (lineid <= 0 || !(sid == 1 || sid == 0) || !(sidepos >=0 || sidepos < 3))
			return;
		
		LineIdIterator TVLineIterator = LineIdIterator.Create(lineid);
		Line li;
		int i;
		while ((i = TVLineIterator.Next()) >= 0)
		{
			li = level.lines[i];
			if (li)
			{
				li.sidedef[sid].SetTexture(sidepos, TexMan.CheckForTexture("TVBROKEN", TexMan.Type_Wall));
			}
		}
		
	} 

	States
	{
		Spawn:
			TNT1 A -1;
			Stop;
		
		Death:
			TNT1 A 0
			{
				if (args[0] > 0 && (args[1] >= 0 && args[1] < 3))
				{
					//ACS_NamedExecuteAlways("DDLib_BreakTVReg", 0, args[0], args[1]);
					A_ChangeTVTextureBroken(args[0], 0, args[1]);
				}
				
				if (args[2] > 0)
					Thing_Remove(args[2]);
				
				Actor mo = null;
				int i;

				for(i = (random[TVDebris]()&3)+3; i; i--)
				{
					mo = Spawn("DD_MetalJunk", Pos, ALLOW_REPLACE);
					if (mo)
					{
						mo.Vel.X = random2[TVDebris]() / 64.;
						mo.Vel.Y = random2[TVDebris]() / 64.;
						mo.Vel.Z = ((random[TVDebris]() & 7) + 5) * 0.75;
					}
				}
				
				A_PlaySound("misc/tcanbreak", CHAN_BODY, 0.7, false, 0.5);
			}
			Stop;
	}
}